<html>
<head>

<title>Groovy Goodness: Preorder And Postorder Tree Traversal</title>

<script language="javascript" src="scripts/shCore.js"></script> 
<script language="javascript" src="scripts/shLegacy.js"></script> 
<script language="javascript" src="scripts/shBrushJava.js"></script> 
<script language="javascript" src="scripts/shBrushXml.js"></script> 
<script language="javascript" src="scripts/shBrushJScript.js"></script> 
<script language="javascript" src="scripts/shBrushGroovy.js"></script> 
<script language="javascript" src="scripts/shBrushPlain.js"></script> 
<script language="javascript" src="scripts/shBrushBash.js"></script> 
 
<link href="styles/reset.css" rel="stylesheet" type="text/css" />
<link href="styles/shCore.css" rel="stylesheet" type="text/css" />
<link type="text/css" rel="stylesheet" href="styles/shThemeRDark.css"/>
<link href="styles/blog.css" rel="stylesheet" type="text/css" />

</head>
<body>

<a href="index.html">Back to index</a>

<h3 class="post-title">Groovy Goodness: Preorder And Postorder Tree Traversal</h3>

<div class="post">
<p>The <code>Node</code> class in Groovy has the methods <code>depthFirst</code> and <code>breadthFirst</code> to return a collection of <code>Node</code> objects using either depth or breadth first traversal. Since Groovy 2.5.0 we can specify if we want to use preorder (the default) or postorder traversal. Also the methods now accept a <code>Closure</code> that will be invoked for each visited node. The <code>Closure</code> has the current <code>Node</code> as first argument, the second argument is the tree level of the current node.</p><p>In the following example we read some XML and then use <code>depthFirst</code> in several ways to visit the tree of nodes:</p><pre class="brush:groovy;">// We start with a XML node hierarchy.
def xml = '''
        &lt;A&gt;
          &lt;B&gt;
            &lt;D/&gt;
            &lt;E/&gt;
          &lt;/B&gt;
          &lt;C&gt;
            &lt;F/&gt;
          &lt;/C&gt;
        &lt;/A&gt;
        '''
def root = new XmlParser().parseText(xml)

// Preorder traversal is default, but
// we can also specify it with the boolean
// argument of depthFirst method.
assert root.depthFirst(true)
           .collect { node -&gt; node.name() } == ['A', 'B', 'D', 'E', 'C', 'F']
           
// Groovy 2.5.0 adds possibility to
// directly call closure for 
// each node visited where the first
// Closure argument is the node and
// the second argument the level.
def result = []
root.depthFirst { node, level -&gt; result &lt;&lt; &quot;$level${node.name()}&quot; }

assert result == ['1A', '2B', '3D', '3E', '2C', '3F']

// Postorder traversal can be specified
// by setting preorder argment to false.
// When used in combination with Closure
// argument we must using named argument
// preorder.
result = []
root.depthFirst(preorder: false) { node -&gt; result &lt;&lt; node.name() }

assert result == ['D', 'E', 'B', 'F', 'C', 'A']</pre><p>In our second example we use the <code>breadthFirst</code> method. This means the nodes for visited per level in the tree:</p><pre class="brush:groovy;">// Let's create a Node hierarchy.
def builder = NodeBuilder.newInstance()
def root = builder.A {
    B {
        D()
        E()
    }
    C {
        F()
    }
}


// Preorder traversal is default, but
// we can also specify it with the boolean
// argument of breadthFirst method.
assert root.breadthFirst(true)
           .collect { node -&gt; node.name() } == ['A', 'B', 'C', 'D', 'E', 'F']
           
// Groovy 2.5.0 adds possibility to
// directly call closure for 
// each node visited with node and level.
def result = []
root.breadthFirst { node, level -&gt; result &lt;&lt; &quot;$level${node.name()}&quot; }

assert result == ['1A', '2B', '2C', '3D', '3E', '3F']

// Postorder traversal is implemented
// as starting at the lowest level and 
// working our way up.
result = []
root.breadthFirst(preorder: false) { node -&gt; result &lt;&lt; node.name() }

assert result == ['D', 'E', 'F', 'B', 'C', 'A']</pre><p>Written with Groovy 2.5.0.</p
</div>

<script language="javascript"> 
SyntaxHighlighter.config.bloggerMode = true;
SyntaxHighlighter.config.clipboardSwf = 'scripts/clipboard.swf';
SyntaxHighlighter.defaults['first-line'] = 0;
SyntaxHighlighter.defaults['auto-links'] = false;
SyntaxHighlighter.all();
dp.SyntaxHighlighter.HighlightAll('code');
</script>

</body>
</html>